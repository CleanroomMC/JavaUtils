plugins {
    id 'java-library'
    id 'maven-publish'
}

group = 'com.cleanroommc'
version = '1.1.5'
base.archivesName = 'java-utils'

java {
    toolchain {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
        languageVersion = JavaLanguageVersion.of(8)
    }
    withSourcesJar()
}

sourceSets {
    api
    checker
    main {
        compileClasspath += api.output
        runtimeClasspath += api.output
        compileClasspath += checker.output
        runtimeClasspath += checker.output
    }
    test {
        compileClasspath += api.output
        runtimeClasspath += api.output
        compileClasspath += checker.output
        runtimeClasspath += checker.output
    }
}

repositories {
    maven {
        name 'CleanroomMC'
        url 'https://maven.cleanroommc.com'
    }
    mavenCentral()
}

dependencies {
    api 'org.slf4j:slf4j-api:2.0.17'
    api 'com.cleanroommc:platform-utils:2.0.0'

    runtimeOnly 'org.slf4j:slf4j-simple:2.0.17'

    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'
}

test {
    useJUnitPlatform()
}

tasks.named('compileCheckerJava', JavaCompile).configure {
    sourceCompatibility = JavaVersion.VERSION_1_2
    targetCompatibility = JavaVersion.VERSION_1_1
    options.compilerArgs << '-Xlint:-options'
}

jar {
    from sourceSets.api.output.classesDirs
    from sourceSets.checker.output.classesDirs
}

tasks.register('apiJar', Jar).configure {
    group 'build'
    archiveClassifier = 'api'

    from sourceSets.api.output.classesDirs
}

tasks.register('apiSourceJar', Jar).configure {
    group 'build'
    archiveClassifier = 'api-source'

    from sourceSets.api.allSource
}

tasks.register('checkerJar', Jar).configure {
    group 'build'
    archiveClassifier = 'checker'

    from sourceSets.checker.output.classesDirs
    from sourceSets.checker.output.resourcesDir

    manifest {
        attributes('Main-Class': 'com.cleanroommc.javautils.checker.JavaChecker')
    }
}

tasks.register('nolocatorJar', Jar).configure {
    group 'build'
    archiveClassifier = 'nolocator'

    from (sourceSets.main.output.classesDirs) {
        exclude 'com/cleanroommc/javautils/locators/**'
    }
    from (sourceSets.api.output.classesDirs) {
        exclude 'com/cleanroommc/javautils/spi/**'
    }
    from sourceSets.checker.output.classesDirs
}

tasks.register('nolocatorSourceJar', Jar).configure {
    group 'build'
    archiveClassifier = 'nolocator-source'

    from (sourceSets.main.allSource) {
        exclude 'com/cleanroommc/javautils/locators/**'
    }
    from (sourceSets.api.allSource) {
        exclude 'com/cleanroommc/javautils/spi/**'
    }
    from sourceSets.checker.allSource
}

publishing {
    repositories {
        maven {
            name 'CleanroomMaven'
            url 'https://repo.cleanroommc.com/releases'
            credentials(PasswordCredentials)
        }
    }
    publications {
        mavenJava(MavenPublication) {
            artifactId 'java-utils'
            from components.java
            artifact tasks.apiJar
            artifact tasks.apiSourceJar
            artifact tasks.checkerJar
            artifact tasks.nolocatorJar
            artifact tasks.nolocatorSourceJar
        }
    }
}
